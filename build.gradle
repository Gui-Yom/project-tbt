plugins {
    id "java"
    id "application"
    id "maven"
    id "com.github.johnrengelman.shadow" version "5.0.0"
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    // JitPack
    maven {
        url "https://jitpack.io"
    }
}

def jlineVersion = "3.11.0"

dependencies {
    implementation "org.jline:jline-terminal:$jlineVersion"
    implementation "org.jline:jline-terminal-jansi:$jlineVersion"
    implementation "org.jline:jline-style:$jlineVersion"
    implementation "org.jline:jline-reader:$jlineVersion"
    implementation "org.jline:jline-remote-telnet:$jlineVersion"
    implementation "com.github.LimeiloN:anscapes:0.4.0"
    implementation "com.google.code.gson:gson:2.8.5"
    //implementation "com.googlecode.lanterna:lanterna:3.0.1"
}

// Define the main class for the application
mainClassName = "lorganisation.projecttbt.Game"
group = "lorganisation"
version = "0.4.0"

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

/*
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}
*/

shadowJar {
    archiveFileName = "project-tbt.jar"
    manifest {
        attributes "Main-Class": mainClassName
    }
}

task copyScripts(type: Copy) {

    from "src/main/dist"
    include "*.sh"
    include "*.bat"
    into "$buildDir/libs"
}

task altConsole(type: Copy) {

    from(zipTree("src/main/dist/ConsoleZ.zip")) {
        include "*"
    }
    into "$buildDir/libs"
}

task play(type: Exec) {

    dependsOn shadowJar
    dependsOn copyScripts

    workingDir "$buildDir/libs"
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        // Windows
        commandLine "C:/Windows/System32/cmd.exe", "/C start cmd /C call start.bat"
    } else {
        // Unix, for XFCE's terminal only
        commandLine "/bin/bash terminal --window --command=\"bash start.sh\" --title=\"Project: TBT\" --hide-menubar --hide-toolbar --hide-borders"
    }
}

task experiment(type: Exec) {

    dependsOn shadowJar

    def mainClass = "lorganisation.projecttbt.experiments.SerializationTest"
    //def mainClass = "lorganisation.projecttbt.experiments.FeederTest"

    workingDir "$buildDir/libs"
    // C:\Windows\System32\cmd.exe /C start cmd /C "echo a && pause"
    commandLine "C:/Windows/System32/cmd.exe", "/C start cmd /K java -cp project-tbt.jar $mainClass"
}

/*
clean.doFirst {
    delete "temp"
}
*/
